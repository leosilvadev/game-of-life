plugins {
	id 'groovy'
	id 'eclipse'
	id 'application'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.6.3'
}

sourceCompatibility = 1.8

mainClassName = "org.gameoflife.Application"
applicationName = "gameOfLife"

repositories { mavenCentral() }

dependencies {
	compile group:'org.codehaus.groovy', name:'groovy-all', version:'2.4.6'
	compile group:'org.springframework.boot', name:'spring-boot-starter-websocket', version:'1.3.5.RELEASE'
	compile group:'org.springframework', name:'spring-messaging', version:'4.2.6.RELEASE'
	
	testCompile group: 'org.springframework', name: 'spring-test', version: '4.2.6.RELEASE'
	testCompile group:'org.gebish', name:'geb-spock', version:'0.10.0'
	testCompile group:'org.seleniumhq.selenium', name:'selenium-support', version:'2.43.1'
	testCompile group:'org.seleniumhq.selenium', name:'selenium-firefox-driver', version:'2.53.0'
	testCompile group:'cglib', name:'cglib-nodep', version:'3.2.2'
	testCompile group:'org.objenesis', name:'objenesis', version:'2.3'
	testCompile group:'org.spockframework', name:'spock-core', version:'1.0-groovy-2.4'
	testCompile group:'org.spockframework', name:'spock-spring', version:'1.0-groovy-2.4'
}

test {
	filter { includeTestsMatching "*UnitSpec" }
}

if ( !System.env.CI ) {
	task "uiTest"(type: Test) {
		filter { includeTestsMatching "*UISpec" }
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': 'game-of-life',
		'Implementation-Version': version
	}
}

jacocoTestReport {
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

task stage(dependsOn: ['clean', 'installApp'])